(ns identity.model-test
  (:require [clojure.test :refer [deftest is]]
            [identity.datom :as d]
            [identity.model :as m]
            [identity.validate :as v]))

(deftest subject-shape
  (let [s (m/subject "did:web:example.com:alice" :person {:labels #{:member}})]
    (is (v/valid? s))
    (is (= :person (:identity.subject/type s)))))

(deftest rejects-adjudicating-and-incomplete-records
  (is (not (v/valid? (m/evidence-ref "e1" :screening {:non-adjudicating false}))))
  (is (thrown? #?(:clj clojure.lang.ExceptionInfo :cljs ExceptionInfo)
               (v/valid! (m/attestation nil "did:web:example.com:alice" :verified {})))))

(deftest emits-attestation-datoms
  (let [a (m/attestation "att1" "did:web:example.com:alice" :verified
                         {:issuer "issuer" :evidence ["e1"]})]
    (is (= [{:db/id "att1"
             :identity.attestation/subject "did:web:example.com:alice"
             :identity.attestation/predicate :verified
             :identity.attestation/issuer "issuer"
             :identity.attestation/evidence ["e1"]
             :identity.attestation/issued-at nil
             :identity/non-adjudicating true}]
           (d/attestation-datoms a)))))
