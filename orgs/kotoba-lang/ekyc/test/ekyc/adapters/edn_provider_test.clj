(ns ekyc.adapters.edn-provider-test
  (:require [clojure.test :refer [deftest is]]
            [ekyc.adapters.edn-provider :as edn-provider]
            [ekyc.adapters.provider :as provider]
            [ekyc.core :as c]
            [ekyc.model :as m]))

(deftest persists-ekyc-provider-and-custody-state
  (let [file (java.io.File/createTempFile "kotoba-ekyc" ".edn")]
    (try
      (.delete file)
      (let [port (provider/provider-port
                  (edn-provider/edn-provider (.getPath file))
                  (edn-provider/edn-custody (.getPath file))
                  {})
            session (m/session "e1" "did:web:example.com:alice"
                               {:provider :edn-provider
                                :required-checks #{:liveness}})
            started (c/start port session)
            evidence (m/evidence started :liveness :submitted
                                 {:source :mobile
                                  :evidence-ref "blob://capture/live"})]
        (is (= :submitted (:ekyc/status started)))
        (is (= :verified (:ekyc/status (c/submit port started evidence))))
        (is (= :verified (:ekyc/status (c/result port started))))
        (let [state (edn-provider/state (.getPath file))]
          (is (= "edn://ekyc/session/e1" (get-in state [:sessions "e1" :session-ref])))
          (is (= 1 (count (:custody state))))
          (is (= :verified (get-in state [:evidence "e1" :liveness :status])))))
      (finally
        (.delete file)))))
