(ns ekyc.adapters.identity-bridge-test
  (:require [clojure.test :refer [deftest is]]
            [ekyc.adapters.identity-bridge :as bridge]
            [ekyc.model :as m]
            [identity.adapters.ledger :as ledger]))

(defn collecting-ledger [txs]
  (reify ledger/ILedger
    (transact! [_ datoms opts]
      (swap! txs conj {:datoms datoms :opts opts})
      {:tx/id (str "tx-" (count @txs))})))

(deftest persists-verified-ekyc-evidence-to-identity-ledger
  (let [txs (atom [])
        ledger (collecting-ledger txs)
        session (m/session "e1" "did:web:example.com:alice"
                           {:provider :provider-x
                            :required-checks #{:liveness :document-authenticity}})
        live (m/evidence session :liveness :verified
                         {:evidence-ref "kagi://evidence/live"
                          :source :mobile
                          :observed-at "2026-07-01T00:00:00Z"})
        doc (m/evidence session :document-authenticity :verified
                        {:evidence-ref "kagi://evidence/doc"
                         :source :passport
                         :observed-at "2026-07-01T00:01:00Z"})
        completion (bridge/persist-completion! ledger session [live doc] {:case-ref "case-1"})]
    (is (:ekyc/complete? completion))
    (is (= ["ekyc:e1:liveness" "ekyc:e1:document-authenticity" "ekyc:e1:attestation"]
           (mapv #(-> % :datoms first :db/id) @txs)))
    (is (= :verified (-> @txs last :datoms first :identity.attestation/predicate)))))

(deftest incomplete-ekyc-persists-review-attestation
  (let [txs (atom [])
        ledger (collecting-ledger txs)
        session (m/session "e2" "did:web:example.com:bob"
                           {:provider :provider-x
                            :required-checks #{:liveness :document-authenticity}})
        live (m/evidence session :liveness :verified
                         {:evidence-ref "kagi://evidence/live"})]
    (is (= :review (:ekyc/status (bridge/persist-completion! ledger session [live] {}))))
    (is (= :review (-> @txs last :datoms first :identity.attestation/predicate)))))
