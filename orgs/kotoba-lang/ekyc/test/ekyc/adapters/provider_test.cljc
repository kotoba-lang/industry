(ns ekyc.adapters.provider-test
  (:require [clojure.test :refer [deftest is]]
            [ekyc.adapters.provider :as a]
            [ekyc.core :as c]
            [ekyc.model :as m]))

(deftest starts-session-and-submits-evidence-through-provider
  (let [calls (atom [])
        client (reify a/IEkycProviderClient
                 (create-session! [_ payload opts]
                   (swap! calls conj [:create payload opts])
                   {:status :submitted
                    :session-ref "provider://session/e1"
                    :provider :provider-x})
                 (upload-evidence! [_ payload opts]
                   (swap! calls conj [:upload payload opts])
                   {:status :verified
                    :evidence-ref "provider://evidence/live"
                    :confidence 990
                    :observed-at "2026-07-01T00:00:00Z"})
                 (retrieve-result! [_ payload opts]
                   (swap! calls conj [:result payload opts])
                   {:status :verified
                    :evidence [{:check :liveness
                                :status :verified
                                :evidence-ref "provider://evidence/live"
                                :confidence 990}]}))
        custody (reify a/IEvidenceCustody
                  (commit-evidence! [_ payload opts]
                    (swap! calls conj [:custody payload opts])
                    {:evidence-ref "kagi://custody/live"}))
        port (a/provider-port client custody {:tenant "t1"})
        session (m/session "e1" "did:web:example.com:alice"
                           {:provider :provider-x
                            :purpose "onboarding"
                            :required-checks #{:liveness}})
        started (c/start port session)
        evidence (m/evidence started :liveness :submitted {:source :mobile
                                                           :evidence-ref "blob://capture/live"})]
    (is (= :submitted (:ekyc/status started)))
    (is (= "provider://session/e1" (:ekyc/session-ref started)))
    (is (= :verified (:ekyc/status (c/submit port started evidence))))
    (is (= :verified (:ekyc/status (c/result port started))))
    (is (= [:create :custody :upload :result] (mapv first @calls)))
    (is (= {:id "e1"
            :subject "did:web:example.com:alice"
            :purpose "onboarding"
            :required-checks #{:liveness}
            :provider :provider-x
            :created-at nil
            :expires-at nil}
           (second (first @calls))))))
