(ns ekyc.adapters.vc-issuer-test
  (:require [clojure.test :refer [deftest is]]
            [ekyc.adapters.vc-issuer :as vc]
            [ekyc.model :as m]))

(deftest issues-vc-for-completed-ekyc-evidence
  (let [issued (atom [])
        issuer (vc/static-issuer issued)
        session (m/session "e1" "did:web:example.com:alice"
                           {:provider :provider-x
                            :required-checks #{:liveness :document-authenticity}})
        live (m/evidence session :liveness :verified
                         {:evidence-ref "kagi://evidence/live"
                          :source :mobile})
        doc (m/evidence session :document-authenticity :verified
                        {:evidence-ref "kagi://evidence/doc"
                         :source :passport})
        out (vc/issue-completion-credential! issuer session [live doc]
                                             {:issuer "did:web:issuer.example"
                                              :issued-at "2026-07-01T00:00:00Z"})]
    (is (= "kagi://vc/ekyc/e1" (:vc/ref out)))
    (is (= :verified (get-in out [:ekyc/completion :ekyc/status])))
    (is (= "did:web:example.com:alice"
           (get-in out [:vc/payload :credentialSubject :id])))
    (is (= #{:liveness :document-authenticity}
           (get-in out [:vc/payload :credentialSubject :ekyc/verified-checks])))
    (is (= 1 (count @issued)))))

(deftest refuses-vc-for-incomplete-ekyc-evidence
  (let [issuer (vc/static-issuer)
        session (m/session "e2" "did:web:example.com:bob"
                           {:required-checks #{:liveness :document-authenticity}})
        live (m/evidence session :liveness :verified
                         {:evidence-ref "kagi://evidence/live"})]
    (is (thrown? #?(:clj clojure.lang.ExceptionInfo :cljs ExceptionInfo)
                 (vc/issue-completion-credential! issuer session [live] {})))))
