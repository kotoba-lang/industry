(ns authentication.adapters.factor-test
  (:require [authentication.adapters.factor :as a]
            [authentication.core :as c]
            [authentication.model :as m]
            [clojure.test :refer [deftest is]]))

(deftest verifies-factor-through-factor-client
  (let [calls (atom [])
        client (reify a/IFactorClient
                 (verify! [_ payload opts]
                   (swap! calls conj [payload opts])
                   {:evidence-ref "kagi://factor/webauthn"
                    :assurance :phishing-resistant
                    :verified-at "2026-07-01T00:00:00Z"}))
        verifier (a/verifier client {:origin "https://rp.example"})
        req (m/factor-request "fr1" :webauthn {:subject "did:web:example.com:alice"
                                               :challenge-ref "kagi://challenge/1"})]
    (is (= {:authn.factor/id "fr1"
            :authn.factor/type :webauthn
            :authn.factor/ok? true
            :authn.factor/subject "did:web:example.com:alice"
            :authn.factor/evidence-ref "kagi://factor/webauthn"
            :authn.factor/assurance :phishing-resistant
            :authn.factor/at "2026-07-01T00:00:00Z"}
           (c/verify-factor {:webauthn verifier} req {:credential-id "cred-1"})))
    (is (= [[{:request-id "fr1"
              :type :webauthn
              :subject "did:web:example.com:alice"
              :challenge-ref "kagi://challenge/1"
              :purpose nil
              :response {:credential-id "cred-1"}}
             {:origin "https://rp.example"}]]
           @calls))))
