(ns onetime.adapters.totp
  (:require [clojure.string :as str]
            [onetime.adapters.digest :as digest])
  (:import [javax.crypto Mac]
           [javax.crypto.spec SecretKeySpec]))

(def ^:private base32-alphabet
  (zipmap "ABCDEFGHIJKLMNOPQRSTUVWXYZ234567" (range)))

(defn base32-decode [s]
  (let [clean (-> s
                  str/upper-case
                  (str/replace #"=+$" ""))
        bits (apply str
                    (map (fn [ch]
                           (let [v (get base32-alphabet ch)]
                             (when (nil? v)
                               (throw (ex-info "invalid base32 character" {:char ch})))
                             (format "%5s" (Integer/toBinaryString v))))
                         clean))
        bits (str/replace bits #" " "0")
        bytes (for [chunk (partition 8 8 nil bits)
                    :when (= 8 (count chunk))]
                (Integer/parseInt (apply str chunk) 2))]
    (byte-array (map unchecked-byte bytes))))

(defn- counter-bytes [counter]
  (byte-array
   (map unchecked-byte
        (for [shift (range 56 -1 -8)]
          (bit-and 0xff (bit-shift-right counter shift))))))

(defn hotp
  ([secret counter] (hotp secret counter 6))
  ([secret counter digits]
   (let [mac (doto (Mac/getInstance "HmacSHA1")
               (.init (SecretKeySpec. secret "HmacSHA1")))
         h (.doFinal mac (counter-bytes counter))
         offset (bit-and (aget h 19) 0x0f)
         binary (bit-or (bit-shift-left (bit-and (aget h offset) 0x7f) 24)
                        (bit-shift-left (bit-and (aget h (inc offset)) 0xff) 16)
                        (bit-shift-left (bit-and (aget h (+ offset 2)) 0xff) 8)
                        (bit-and (aget h (+ offset 3)) 0xff))
         modulus (long (Math/pow 10 digits))]
     (format (str "%0" digits "d") (mod binary modulus)))))

(defn- resolve-secret [opts digest-ref]
  (let [resolver (:secret-resolver opts)
        secret (if resolver (resolver digest-ref) (:secret opts))]
    (cond
      (bytes? secret) secret
      (string? secret) (base32-decode secret)
      :else (throw (ex-info "missing one-time secret" {:digest-ref digest-ref})))))

(defn- totp-counters [now step window]
  (let [base (quot now step)]
    (range (- base window) (+ base window 1))))

(defn- valid-code? [secret code counters digits]
  (boolean (some #(= code (hotp secret % digits)) counters)))

(defn verifier [opts]
  (reify digest/IDigestVerifier
    (verify-digest! [_ payload call-opts]
      (let [opts (merge opts call-opts)
            secret (resolve-secret opts (:digest-ref payload))
            code (str (get-in payload [:response :code]))
            digits (get opts :digits 6)
            counters (case (:kind payload)
                       :hotp [(or (get-in payload [:response :counter]) (:counter opts))]
                       :totp (totp-counters (or (:now opts) (quot (System/currentTimeMillis) 1000))
                                            (get opts :time-step 30)
                                            (get opts :window 1)))]
        (if (valid-code? secret code counters digits)
          {:verified-at (:verified-at opts)}
          {:error :invalid-code
           :verified-at (:verified-at opts)})))))
