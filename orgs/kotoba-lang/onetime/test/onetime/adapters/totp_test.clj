(ns onetime.adapters.totp-test
  (:require [clojure.test :refer [deftest is]]
            [onetime.adapters.digest :as digest]
            [onetime.adapters.totp :as totp]
            [onetime.core :as c]
            [onetime.model :as m]))

(def rfc-secret "GEZDGNBVGY3TQOJQGEZDGNBVGY3TQOJQ")

(deftest hotp-matches-rfc4226-vector
  (is (= "755224" (totp/hotp (totp/base32-decode rfc-secret) 0 6)))
  (is (= "287082" (totp/hotp (totp/base32-decode rfc-secret) 1 6))))

(deftest verifies-hotp-through-digest-port
  (let [verifier (totp/verifier {:secret-resolver (constantly rfc-secret)
                                 :verified-at "2026-07-01T00:00:00Z"})
        port (digest/verifier-port verifier {})
        challenge (m/challenge "hotp1" :hotp {:digest-ref "kagi://secret/hotp"})]
    (is (:onetime/ok? (c/verify port challenge {:code "755224" :counter 0})))
    (is (false? (:onetime/ok? (c/verify port challenge {:code "000000" :counter 0}))))))

(deftest verifies-totp-through-digest-port
  (let [verifier (totp/verifier {:secret-resolver (constantly rfc-secret)
                                 :now 59
                                 :time-step 30
                                 :window 0
                                 :digits 8})
        port (digest/verifier-port verifier {})
        challenge (m/challenge "totp1" :totp {:digest-ref "kagi://secret/totp"})]
    (is (:onetime/ok? (c/verify port challenge {:code "94287082"})))))
