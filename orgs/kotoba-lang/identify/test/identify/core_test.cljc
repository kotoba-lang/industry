(ns identify.core-test
  (:require [clojure.test :refer [deftest is]]
            [identify.core :as c]
            [identify.model :as m]
            [identify.ports :as p]))

(deftest resolves-candidate
  (let [port (reify p/IIdentify
               (resolve-candidates [_ _]
                 [(m/candidate "did:web:example.com:bob" 700 {:asserter "test"})
                  (m/candidate "did:web:example.com:alice" 900 {:asserter "test"})]))
        out (c/identify port (m/identifier :email "a@example.com" {}))]
    (is (= 2 (count out)))
    (is (= "did:web:example.com:alice" (:identify.candidate/subject (first out))))))

(deftest rejects-invalid-identifier-and-candidate
  (let [bad-port (reify p/IIdentify
                   (resolve-candidates [_ _]
                     [(m/candidate "did:web:example.com:alice" 1200 {})]))]
    (is (thrown? #?(:clj clojure.lang.ExceptionInfo :cljs ExceptionInfo)
                 (c/identify bad-port (m/identifier :unknown "a@example.com" {}))))
    (is (thrown? #?(:clj clojure.lang.ExceptionInfo :cljs ExceptionInfo)
                 (c/identify bad-port (m/identifier :email "a@example.com" {}))))))

(deftest selects-top-candidate-above-threshold
  (let [port (reify p/IIdentify
               (resolve-candidates [_ _]
                 [(m/candidate "did:web:example.com:bob" 700 {})
                  (m/candidate "did:web:example.com:alice" 900 {})]))]
    (is (= "did:web:example.com:alice"
           (:identify.candidate/subject
            (c/top-candidate port (m/identifier :email "a@example.com" {}) 800))))))

(deftest calibrates-confidence-by-source
  (let [candidate (m/candidate "did:web:example.com:alice" 900
                               {:source :email-resolver})]
    (is (= 720 (:identify.candidate/confidence
                (c/calibrate-confidence candidate {:email-resolver 0.8}))))
    (is (= 1000 (:identify.candidate/confidence
                 (c/calibrate-confidence candidate {:email-resolver (fn [_ _] 1200)}))))))

(deftest merges-duplicate-subject-candidates-and-evidence
  (let [out (c/merge-candidates
             [(m/candidate "did:web:example.com:alice" 800
                           {:source :email
                            :evidence-ref "kagi://evidence/email"})
              (m/candidate "did:web:example.com:alice" 900
                           {:source :wallet
                            :evidence-ref "kagi://evidence/wallet"})
              (m/candidate "did:web:example.com:bob" 400
                           {:source :device
                            :evidence-ref "kagi://evidence/device"})])]
    (is (= 2 (count out)))
    (is (= "did:web:example.com:alice" (:identify.candidate/subject (first out))))
    (is (= ["kagi://evidence/wallet" "kagi://evidence/email"]
           (:identify.candidate/evidence-refs (first out))))
    (is (= 2 (count (:identify.candidate/assertions (first out)))))))

(deftest flags-conflicting-top-candidates-after-calibration
  (let [port (reify p/IIdentify
               (resolve-candidates [_ _]
                 [(m/candidate "did:web:example.com:alice" 910 {:source :email})
                  (m/candidate "did:web:example.com:bob" 900 {:source :wallet})
                  (m/candidate "did:web:example.com:carol" 700 {:source :device})]))
        out (c/identify-merged port
                               (m/identifier :email "a@example.com" {})
                               {:conflict-delta 20
                                :calibration {:email 1.0 :wallet 1.0 :device 1.0}})]
    (is (= [true true false]
           (mapv :identify.candidate/conflict? out)))))
