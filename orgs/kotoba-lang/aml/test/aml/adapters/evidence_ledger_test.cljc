(ns aml.adapters.evidence-ledger-test
  (:require [aml.adapters.evidence-ledger :as evidence-ledger]
            [aml.model :as m]
            [clojure.test :refer [deftest is]]
            [identity.adapters.ledger :as ledger]))

(defn- recording-ledger [txs]
  (reify ledger/ILedger
    (transact! [_ datoms opts]
      (let [tx {:tx/id (str "tx-" (inc (count @txs)))
                :tx/datoms (count datoms)
                :tx/case-ref (:case-ref opts)
                :tx/datoms* datoms}]
        (swap! txs conj tx)
        tx))))

(deftest maps-aml-result-into-identity-evidence-and-attestation
  (let [request (m/request "aml-1" "did:web:example.com:alice" {:case-ref "case-1"})
        result (m/result request :malak :review
                         {:score 0.72
                          :categories #{:sanctions}
                          :evidence-ref "kagi://aml/evidence/1"
                          :asserter "malak"
                          :observed-at "2026-07-01T00:00:00Z"})
        evidence (evidence-ledger/result->evidence request result)
        attestation (evidence-ledger/result->attestation request result evidence)]
    (is (= :screening (:identity.evidence/kind evidence)))
    (is (= "kagi://aml/evidence/1" (:identity.evidence/ref evidence)))
    (is (= :aml/review (:identity.attestation/predicate attestation)))
    (is (= ["aml-1:malak:evidence"] (:identity.attestation/evidence attestation)))))

(deftest persists-aml-screening-evidence-to-identity-ledger
  (let [txs (atom [])
        l (recording-ledger txs)
        request (m/request "aml-2" "did:web:example.com:alice" {:case-ref "case-2"
                                                                :routes [:yabai :malak]})
        screening {:aml/results [(m/result request :yabai :clear
                                           {:evidence-ref "kagi://aml/evidence/yabai"
                                            :asserter "yabai"})
                                 (m/result request :malak :review
                                           {:evidence-ref "kagi://aml/evidence/malak"
                                            :asserter "malak"})]}]
    (is (= 2 (count (evidence-ledger/persist-screening! l request screening {:case-ref "case-2"}))))
    (is (= [1 1 1 1] (mapv :tx/datoms @txs)))
    (is (= ["aml-2:yabai:evidence" "aml-2:yabai:attestation"
            "aml-2:malak:evidence" "aml-2:malak:attestation"]
           (mapv (comp :db/id first :tx/datoms*) @txs)))))
