(ns aml.adapters.retry-test
  (:require [aml.adapters.etzhayyim :as etzhayyim]
            [aml.adapters.retry :as retry]
            [clojure.test :refer [deftest is]]))

(deftest retries-retryable-xrpc-results
  (let [calls (atom 0)
        client (reify etzhayyim/IXrpcClient
                 (invoke! [_ _ _]
                   (let [n (swap! calls inc)]
                     (if (< n 3)
                       {:error :http/status :status 503}
                       {:score 0}))))
        wrapped (retry/retry-client client {:attempts 3})]
    (is (= {:score 0} (etzhayyim/invoke! wrapped "ns" {})))
    (is (= 3 @calls))))

(deftest does-not-retry-non-retryable-xrpc-results
  (let [calls (atom 0)
        client (reify etzhayyim/IXrpcClient
                 (invoke! [_ _ _]
                   (swap! calls inc)
                   {:error :invalid-request :status 400}))
        wrapped (retry/retry-client client {:attempts 3})]
    (is (= {:error :invalid-request :status 400}
           (etzhayyim/invoke! wrapped "ns" {})))
    (is (= 1 @calls))))

(deftest records-exponential-backoff-delays
  (let [calls (atom 0)
        sleeps (atom [])
        client (reify etzhayyim/IXrpcClient
                 (invoke! [_ _ _]
                   (let [n (swap! calls inc)]
                     (if (< n 3)
                       {:error :http/status :status 503}
                       {:score 0}))))
        wrapped (retry/retry-client client {:attempts 3
                                            :base-delay-ms 25
                                            :sleep! (fn [delay context]
                                                      (swap! sleeps conj [delay (:attempt context)]))})]
    (is (= {:score 0} (etzhayyim/invoke! wrapped "ns" {})))
    (is (= [[25 1] [50 2]] @sleeps))))
