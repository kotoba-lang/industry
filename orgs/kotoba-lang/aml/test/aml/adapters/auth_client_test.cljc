(ns aml.adapters.auth-client-test
  (:require [aml.adapters.auth-client :as auth-client]
            [aml.adapters.etzhayyim :as etzhayyim]
            [clojure.test :refer [deftest is]]))

(deftest adds-bearer-token-header-through-payload-metadata
  (let [calls (atom [])
        client (reify etzhayyim/IXrpcClient
                 (invoke! [_ nsid payload]
                   (swap! calls conj {:nsid nsid
                                      :payload payload
                                      :metadata (meta payload)})
                   {:score 0}))
        wrapped (auth-client/auth-client client {:method :bearer :token "jwt"})]
    (is (= {:score 0}
           (etzhayyim/invoke! wrapped "ai.gftd.apps.yabai.getRisk" {:entityId "alice"})))
    (is (= {"Authorization" "Bearer jwt"} (get-in @calls [0 :metadata :headers])))
    (is (= {:entityId "alice"} (get-in @calls [0 :payload])))))

(deftest adds-api-key-header-and-redacts-secret
  (let [calls (atom [])
        client (reify etzhayyim/IXrpcClient
                 (invoke! [_ _ payload]
                   (swap! calls conj (meta payload))
                   {:score 0}))
        wrapped (auth-client/auth-client client {:method :api-key
                                                 :header "X-Etzhayyim-Key"
                                                 :api-key "secret"})]
    (etzhayyim/invoke! wrapped "ns" {})
    (is (= {"X-Etzhayyim-Key" "secret"} (get-in @calls [0 :headers])))
    (is (= {:method :api-key :header "X-Etzhayyim-Key" :redacted? true}
           (auth-client/redact-auth {:method :api-key
                                     :header "X-Etzhayyim-Key"
                                     :api-key "secret"})))))
