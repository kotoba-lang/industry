(ns aml.adapters.xrpc-http-test
  (:require [aml.adapters.etzhayyim]
            [aml.adapters.xrpc-http :as xrpc]
            [aml.core :as c]
            [aml.model :as m]
            [clojure.edn :as edn]
            [clojure.test :refer [deftest is]])
  (:import [com.sun.net.httpserver HttpHandler HttpServer]
           [java.net InetSocketAddress]))

(defn- respond! [exchange status body]
  (let [bytes (.getBytes body "UTF-8")]
    (.sendResponseHeaders exchange status (alength bytes))
    (with-open [out (.getResponseBody exchange)]
      (.write out bytes))))

(defn- server [requests]
  (let [s (HttpServer/create (InetSocketAddress. "127.0.0.1" 0) 0)]
    (.createContext
     s "/"
     (reify HttpHandler
       (handle [_ exchange]
         (let [path (.getPath (.getRequestURI exchange))
               body (slurp (.getRequestBody exchange))
               auth (some-> (.getRequestHeaders exchange) (.getFirst "Authorization"))]
           (swap! requests conj [path (edn/read-string body) auth])
           (respond! exchange 200 (pr-str {:score 710
                                           :flags [:pep]
                                           :request-id "x1"}))))))
    (.start s)
    s))

(defn- base-url [^HttpServer s]
  (str "http://127.0.0.1:" (.getPort (.getAddress s))))

(deftest live-xrpc-client-posts-aml-risk-request
  (let [requests (atom [])
        s (server requests)]
    (try
      (let [client (xrpc/xrpc-client (base-url s) {})
            port (aml.adapters.etzhayyim/screening-port client)
            req (m/request "aml1" {:subject/id "did:web:example.com:alice"}
                           {:case-ref "case-1" :routes [:yabai]})
            out (c/screen port req)]
        (is (= :review (:aml/status out)))
        (is (= ["/xrpc/ai.gftd.apps.yabai.getRisk"] (mapv first @requests)))
        (is (= "did:web:example.com:alice" (get-in @requests [0 1 :entityId]))))
      (finally
        (.stop s 0)))))

(deftest live-xrpc-client-sends-auth-metadata-headers
  (let [requests (atom [])
        s (server requests)]
    (try
      (let [client (xrpc/xrpc-client (base-url s) {})]
        (is (= {:score 710 :flags [:pep] :request-id "x1"}
               (aml.adapters.etzhayyim/invoke! client "ai.gftd.apps.yabai.getRisk"
                                               (with-meta {:entityId "alice"}
                                                 {:headers {"Authorization" "Bearer jwt"}}))))
        (is (= "Bearer jwt" (get-in @requests [0 2]))))
      (finally
        (.stop s 0)))))
