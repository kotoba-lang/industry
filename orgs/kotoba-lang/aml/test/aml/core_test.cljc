(ns aml.core-test
  (:require [clojure.test :refer [deftest is]]
            [aml.core :as c]
            [aml.datom :as d]
            [aml.model :as m]
            [aml.ports :as p]))

(deftest routes-to-yabai-and-malak
  (let [calls (atom [])
        req (m/request "a1" {:subject/id "did:web:example.com:alice"}
                       {:purpose "onboarding" :case-ref "case-1"})
        port (reify p/IAmlScreening
               (screen! [_ request route]
                 (swap! calls conj route)
                 (m/result request route (if (= route :yabai) :monitor :clear) {:asserter (name route)})))
        out (c/screen port req)]
    (is (= [:yabai :malak] @calls))
    (is (= :review (:aml/status out)))
    (is (= "case-1" (:aml/case-ref out)))))

(deftest rejects-unknown-route-and-missing-case-ref
  (let [port (reify p/IAmlScreening
               (screen! [_ _ _] (throw (ex-info "unexpected" {}))))]
    (is (thrown? #?(:clj clojure.lang.ExceptionInfo :cljs ExceptionInfo)
                 (c/screen port (m/request "a2" {:subject/id "s"} {:routes [:other]}))))
    (is (thrown? #?(:clj clojure.lang.ExceptionInfo :cljs ExceptionInfo)
                 (c/screen port (m/request "a3" {:subject/id "s"} {}))))))

(deftest emits-aml-datoms
  (let [req (m/request "a1" {:subject/id "s"} {:case-ref "case-1"})
        result (m/result req :yabai :monitor {:asserter "yabai"})
        screening {:aml/id "a1"
                   :aml/status :review
                   :aml/case-ref "case-1"
                   :aml/routes [:yabai :malak]}]
    (is (= "a1:yabai" (:db/id (first (d/result-datoms result)))))
    (is (true? (:aml/non-adjudicating (first (d/screening-datoms screening)))))))
