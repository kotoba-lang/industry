(ns ctf.adapters.retry
  (:require [ctf.adapters.etzhayyim :as etzhayyim]))

(defn- retryable? [result]
  (or (= :http/status (:error result))
      (= :transport (:error result))
      (and (:status result) (>= (:status result) 500))))

(defn backoff-delay-ms [attempt opts]
  (let [base (or (:base-delay-ms opts) 100)
        max-delay (or (:max-delay-ms opts) 5000)]
    (min max-delay (* base (long #?(:clj (Math/pow 2 (dec attempt))
                                    :cljs (.pow js/Math 2 (dec attempt))))))))

(defn- with-retry [f attempts opts]
  (loop [n 1]
    (let [result (f)]
      (if (and (< n attempts) (retryable? result))
        (do
          (when-let [sleep! (:sleep! opts)]
            (sleep! (backoff-delay-ms n opts) {:attempt n :result result}))
          (recur (inc n)))
        result))))

(defn retry-client
  ([client] (retry-client client {}))
  ([client opts]
   (let [attempts (max 1 (or (:attempts opts) 3))]
     (reify etzhayyim/IXrpcClient
       (invoke! [_ nsid payload]
         (with-retry #(etzhayyim/invoke! client nsid payload) attempts opts))))))
