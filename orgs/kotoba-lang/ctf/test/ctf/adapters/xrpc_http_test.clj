(ns ctf.adapters.xrpc-http-test
  (:require [clojure.edn :as edn]
            [clojure.test :refer [deftest is]]
            [ctf.adapters.etzhayyim]
            [ctf.adapters.xrpc-http :as xrpc]
            [ctf.core :as c]
            [ctf.model :as m])
  (:import [com.sun.net.httpserver HttpHandler HttpServer]
           [java.net InetSocketAddress]))

(defn- respond! [exchange status body]
  (let [bytes (.getBytes body "UTF-8")]
    (.sendResponseHeaders exchange status (alength bytes))
    (with-open [out (.getResponseBody exchange)]
      (.write out bytes))))

(defn- server [requests]
  (let [s (HttpServer/create (InetSocketAddress. "127.0.0.1" 0) 0)]
    (.createContext
     s "/"
     (reify HttpHandler
       (handle [_ exchange]
         (let [path (.getPath (.getRequestURI exchange))
               body (slurp (.getRequestBody exchange))
               auth (some-> (.getRequestHeaders exchange) (.getFirst "Authorization"))]
           (swap! requests conj [path (edn/read-string body) auth])
           (respond! exchange 200 (pr-str {:severity :high
                                           :riskSignals [:mixer]
                                           :request-id "x1"}))))))
    (.start s)
    s))

(defn- base-url [^HttpServer s]
  (str "http://127.0.0.1:" (.getPort (.getAddress s))))

(deftest live-xrpc-client-posts-ctf-risk-request
  (let [requests (atom [])
        s (server requests)]
    (try
      (let [client (xrpc/xrpc-client (base-url s) {})
            port (ctf.adapters.etzhayyim/screening-port client)
            req (m/request "ctf1" {:wallet/address "wallet:0xabc"}
                           {:case-ref "case-1" :routes [:malak]})
            out (c/screen port req)]
        (is (= :review (:ctf/status out)))
        (is (= ["/xrpc/ai.gftd.apps.malak.queryRiskChain"] (mapv first @requests)))
        (is (= {:wallet/address "wallet:0xabc"} (get-in @requests [0 1 :address]))))
      (finally
        (.stop s 0)))))

(deftest live-xrpc-client-sends-auth-metadata-headers
  (let [requests (atom [])
        s (server requests)]
    (try
      (let [client (xrpc/xrpc-client (base-url s) {})]
        (is (= {:severity :high :riskSignals [:mixer] :request-id "x1"}
               (ctf.adapters.etzhayyim/invoke! client "ai.gftd.apps.malak.queryRiskChain"
                                               (with-meta {:address "wallet"}
                                                 {:headers {"Authorization" "Bearer jwt"}}))))
        (is (= "Bearer jwt" (get-in @requests [0 2]))))
      (finally
        (.stop s 0)))))
