(ns ctf.adapters.auth-client-test
  (:require [clojure.test :refer [deftest is]]
            [ctf.adapters.auth-client :as auth-client]
            [ctf.adapters.etzhayyim :as etzhayyim]))

(deftest adds-bearer-token-header-through-payload-metadata
  (let [calls (atom [])
        client (reify etzhayyim/IXrpcClient
                 (invoke! [_ nsid payload]
                   (swap! calls conj {:nsid nsid
                                      :payload payload
                                      :metadata (meta payload)})
                   {:severity :low}))
        wrapped (auth-client/auth-client client {:method :bearer :token "jwt"})]
    (is (= {:severity :low}
           (etzhayyim/invoke! wrapped "ai.gftd.apps.malak.queryRiskChain" {:address "wallet"})))
    (is (= {"Authorization" "Bearer jwt"} (get-in @calls [0 :metadata :headers])))
    (is (= {:address "wallet"} (get-in @calls [0 :payload])))))

(deftest adds-api-key-header-and-redacts-secret
  (let [calls (atom [])
        client (reify etzhayyim/IXrpcClient
                 (invoke! [_ _ payload]
                   (swap! calls conj (meta payload))
                   {:severity :low}))
        wrapped (auth-client/auth-client client {:method :api-key
                                                 :header "X-Etzhayyim-Key"
                                                 :api-key "secret"})]
    (etzhayyim/invoke! wrapped "ns" {})
    (is (= {"X-Etzhayyim-Key" "secret"} (get-in @calls [0 :headers])))
    (is (= {:method :api-key :header "X-Etzhayyim-Key" :redacted? true}
           (auth-client/redact-auth {:method :api-key
                                     :header "X-Etzhayyim-Key"
                                     :api-key "secret"})))))
