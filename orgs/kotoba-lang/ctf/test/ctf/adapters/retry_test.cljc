(ns ctf.adapters.retry-test
  (:require [clojure.test :refer [deftest is]]
            [ctf.adapters.etzhayyim :as etzhayyim]
            [ctf.adapters.retry :as retry]))

(deftest retries-retryable-xrpc-results
  (let [calls (atom 0)
        client (reify etzhayyim/IXrpcClient
                 (invoke! [_ _ _]
                   (let [n (swap! calls inc)]
                     (if (< n 2)
                       {:error :http/status :status 502}
                       {:severity :low}))))
        wrapped (retry/retry-client client {:attempts 3})]
    (is (= {:severity :low} (etzhayyim/invoke! wrapped "ns" {})))
    (is (= 2 @calls))))

(deftest does-not-retry-non-retryable-xrpc-results
  (let [calls (atom 0)
        client (reify etzhayyim/IXrpcClient
                 (invoke! [_ _ _]
                   (swap! calls inc)
                   {:error :invalid-request :status 400}))
        wrapped (retry/retry-client client {:attempts 3})]
    (is (= {:error :invalid-request :status 400}
           (etzhayyim/invoke! wrapped "ns" {})))
    (is (= 1 @calls))))

(deftest records-exponential-backoff-delays
  (let [calls (atom 0)
        sleeps (atom [])
        client (reify etzhayyim/IXrpcClient
                 (invoke! [_ _ _]
                   (let [n (swap! calls inc)]
                     (if (< n 3)
                       {:error :http/status :status 503}
                       {:severity :low}))))
        wrapped (retry/retry-client client {:attempts 3
                                            :base-delay-ms 25
                                            :sleep! (fn [delay context]
                                                      (swap! sleeps conj [delay (:attempt context)]))})]
    (is (= {:severity :low} (etzhayyim/invoke! wrapped "ns" {})))
    (is (= [[25 1] [50 2]] @sleeps))))
