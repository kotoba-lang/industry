(ns ctf.adapters.evidence-ledger-test
  (:require [clojure.test :refer [deftest is]]
            [ctf.adapters.evidence-ledger :as evidence-ledger]
            [ctf.model :as m]
            [identity.adapters.ledger :as ledger]))

(defn- recording-ledger [txs]
  (reify ledger/ILedger
    (transact! [_ datoms opts]
      (let [tx {:tx/id (str "tx-" (inc (count @txs)))
                :tx/datoms (count datoms)
                :tx/case-ref (:case-ref opts)
                :tx/datoms* datoms}]
        (swap! txs conj tx)
        tx))))

(deftest maps-ctf-result-into-identity-evidence-and-attestation
  (let [request (m/request "ctf-1" "did:web:example.com:alice" {:case-ref "case-1"})
        result (m/result request :malak :challenge
                         {:score 0.61
                          :categories #{:terror-finance}
                          :evidence-ref "kagi://ctf/evidence/1"
                          :asserter "malak"
                          :observed-at "2026-07-01T00:00:00Z"})
        evidence (evidence-ledger/result->evidence request result)
        attestation (evidence-ledger/result->attestation request result evidence)]
    (is (= :screening (:identity.evidence/kind evidence)))
    (is (= "kagi://ctf/evidence/1" (:identity.evidence/ref evidence)))
    (is (= :ctf/challenge (:identity.attestation/predicate attestation)))
    (is (= ["ctf-1:malak:evidence"] (:identity.attestation/evidence attestation)))))

(deftest persists-ctf-screening-evidence-to-identity-ledger
  (let [txs (atom [])
        l (recording-ledger txs)
        request (m/request "ctf-2" "did:web:example.com:alice" {:case-ref "case-2"
                                                                :routes [:malak :yabai]})
        screening {:ctf/results [(m/result request :malak :clear
                                           {:evidence-ref "kagi://ctf/evidence/malak"
                                            :asserter "malak"})
                                 (m/result request :yabai :review
                                           {:evidence-ref "kagi://ctf/evidence/yabai"
                                            :asserter "yabai"})]}]
    (is (= 2 (count (evidence-ledger/persist-screening! l request screening {:case-ref "case-2"}))))
    (is (= [1 1 1 1] (mapv :tx/datoms @txs)))
    (is (= ["ctf-2:malak:evidence" "ctf-2:malak:attestation"
            "ctf-2:yabai:evidence" "ctf-2:yabai:attestation"]
           (mapv (comp :db/id first :tx/datoms*) @txs)))))
