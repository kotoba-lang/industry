(ns ctf.core-test
  (:require [clojure.test :refer [deftest is]]
            [ctf.core :as c]
            [ctf.datom :as d]
            [ctf.model :as m]
            [ctf.ports :as p]))

(deftest routes-to-malak-and-yabai
  (let [calls (atom [])
        req (m/request "c1" {:subject/id "wallet:0xabc"}
                       {:purpose "transfer-screening" :case-ref "case-1"})
        port (reify p/ICtfScreening
               (screen! [_ request route]
                 (swap! calls conj route)
                 (m/result request route (if (= route :malak) :challenge :clear) {:asserter (name route)})))
        out (c/screen port req)]
    (is (= [:malak :yabai] @calls))
    (is (= :review (:ctf/status out)))
    (is (= "case-1" (:ctf/case-ref out)))))

(deftest rejects-unknown-route-and-missing-case-ref
  (let [port (reify p/ICtfScreening
               (screen! [_ _ _] (throw (ex-info "unexpected" {}))))]
    (is (thrown? #?(:clj clojure.lang.ExceptionInfo :cljs ExceptionInfo)
                 (c/screen port (m/request "c2" {:subject/id "s"} {:routes [:other]}))))
    (is (thrown? #?(:clj clojure.lang.ExceptionInfo :cljs ExceptionInfo)
                 (c/screen port (m/request "c3" {:subject/id "s"} {}))))))

(deftest emits-ctf-datoms
  (let [req (m/request "c1" {:subject/id "s"} {:case-ref "case-1"})
        result (m/result req :malak :challenge {:asserter "malak"})
        screening {:ctf/id "c1"
                   :ctf/status :review
                   :ctf/case-ref "case-1"
                   :ctf/routes [:malak :yabai]}]
    (is (= "c1:malak" (:db/id (first (d/result-datoms result)))))
    (is (true? (:ctf/non-adjudicating (first (d/screening-datoms screening)))))))
